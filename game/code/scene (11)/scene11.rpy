label scene_eleven:

    play music "audio/Сцена 11/Катакомби Амбієнт I.mp3"
    scene катакомби_1 with dissolve

    "Не вірю своїм очам"

    "Під землею на мене чекає ціле підземне містечко"

    "Стоїть гомін голосів. Це й було звуком, який я почула раніше"

    "Все довкола зроблене зі сміття. Зшито зі шматків тканини, зкреплено з залізяк"

    "По закутках на матрацах та просто на підлозі сидять люди"

    "Дуже бліді. Майже в усіх дреди подекуди стирчать з блискучих поголених голів"

    "Такі самі, як у Йербі"

    "Вдягнені зовсім не так, як люди в Оазі. Але й не так, як люди на периферії міста. А ще цей сморід…"

    "Запах поту. Йербі сказав, що тут живуть ті, хто виконує найбруднішу роботу в місті"

    "Що він мав на увазі?"

    "Повз мене проходять двоє хлопців"

    хлопець1 "Ще одна прибулиця з поверхні…"

    хлопець2 "Що тобі тут треба? Краще забирайся звідси"

    хлопець1 "Геть!"

    "Він різко нахиляється до мене й загрозливо піднімає свої довгі худі руки, намагаючись мене нажахати. Сміються з мого подиву"

    "А потім вони йдуть. Але я встигла дещо помітити на його руках, витягнутих з рукавів балахону"

    "Шрами"

    "Я придивляюся до людей і бачу, що деякі з них сидять, огорнувши руками коліна, і похитуються"

    "А дехто лежить, обхопивши руками голову"

    "Синдром відміни?"

    "Але я бачила абстиненцію. І у деяких із цих людей вона, м'яко кажучи, нетипова"

    "Що ж тут в біса відбувається?!"

    show генто нейтральний at right2 with dissolve
    show павка здивована at left2  with dissolve
    невідомий "Металу — корозія"

    show павка розгублена at left2
    "Ніколи б не подумала, що ці слова можуть колись викликати в мене таке полегшення!"

    павка "Людям — кайф?"

    show павка усміхнена at left2
    павка "Ви тут, щоб допомогти мені покинути Купол?"

    show генто посмішка at right2
    невідомий "Свобода від Куполу! Саме так. Мене, до речі, звати Генто"

    show павка задумлива at left2
    "Я повинна дізнатися про те, що тут відбувається"

    show павка рішуча at left2
    павка "Люди тут виглядають навіть гірше, ніж ті, кому не вистачає годіуму"

    show генто невдоволений at right2
    генто "Тихше. Тут не слід казати слово 'годіум'. Ти тут туристка — тож не лізь у наші справи. Йди за мною. Я виведу тебе за межі Куполу"

    show генто нейтральний at right2
    show павка задумлива at left2
    "Мені нема чого втрачати. Я не можу покинути Купол, не розгадавши його загадку"

    "Адже це місто створило мене"

    show павка рішуча at left2
    павка "Стій"

    show генто невдоволений at right2
    генто "Ні"

    павка "Я хочу дізнатися правду. Перед тим, як я заберуся з цього міста, я хочу дізнатися правду"

    show генто усміхнений at right2
    генто "Правду… Нащо вона тобі?"
    show павка рішуча at left2
    menu:
        "Мені набридло жити в темряві":
            show генто нейтральний at right2

            павка "Все моє життя мене оточувала брехня. Світ, виліплений з брехні"

            show павка рішуча at left2
            павка "Тепер я хочу знати про цей світ все, що тільки можна"

            show генто усміхнений at right2
            генто "І не лячно тобі?"

            show павка розгублена at left2
            павка "Чесно кажучи, трохи лячно"

            show павка рішуча at left2
            павка "Але я готова"

        "Я хочу знати, як полегшити їх страждання":
            show генто нейтральний at right2
            show павка рішуча at left2
            павка "Ці люди страждають, і я хочу знати, що можна зробити, щоб їм допомогти"

            show генто сумний at right2
            генто "А якщо їм вже нічим не можна допомогти? Що тоді?"

            show павка весела at left2
            павка "Надія все одно залишається. Хіба не для цього існує Антарес? Щоб допомагати людям не втрачати надію?"

        "Це якось пов’язано з тим, що я шукаю":
            show генто нейтральний at right2
            show павка задумлива at left2
            павка "Це все пов’язано, я відчуваю: годіум, музика, страждання цих людей…"

            show павка рішуча at left2
            павка "Мені важливо це зрозуміти, перш ніж я рушатиму далі"

            show генто усміхнений at right2
            генто "То в тебе є місія, е? Що ж, сподіваюсь, ти знайдеш, що шукаєш"

    show генто нейтральний at right2
    show павка рішуча at left2
    генто "Гаразд, я покажу тобі правду. Але попереджаю, що вона може тобі не сподобатися. Ти впевнена, що зможеш з цим далі жити?"

    павка "Впевнена, як ніколи"

    show павка нейтральна at left2
    show генто усміхнений at right2
    генто "Гаразд, тоді йди за мною. Зараз якраз обідня перерва, і ми зможемо пройти в робочу залу непоміченими"

    show генто нейтральний at right2
    "Ми йдемо у бічний тунель, який закінчується величезними металевими дверима"

    "За дверима величезна напівтемна зала, заставлена старими, подряпаними стільцями"

    "Зі стелі звисають шоломи дивної форми. З зовнішньої сторони до них приєднані дроти"

    show генто сумний at right2
    генто "Це шоломи для забору гормонів радості та задоволення. А ось це…"

    "І  нахиляється, щоб показати меня свою зачіску. Тицяє пальцем у свої дреди"

    show генто нейтральний at right2
    show павка здивована at left2
    "Брови в мене повзуть наверх. Я кілька секунд роздивляюся те, що мені здалося косичками"

    "Трубки, під'єднані до черепа. Доторкаюся пальцем до однієї. Холодний пластик відбиває світло"

    генто "…роз'єми для під'єднання шоломів"

    show павка здивована at left2
    павка "…"

    show павка налякана at left2
    павка "Вони роблять годіум з людей???"

    show генто усміхнений at right2
    генто "Технічно він зроблений з гормонів, які виробляють наші тіла"

    show генто сумний at right2
    генто "Але ти правильно все зрозуміла. Ваше зілля видобувають з наших мізків"

    show генто нейтральний at right2
    павка "Я… Я думала, що він синтетичний"

    show павка розгублена at left2
    "Чорт забирай… Лайя був правий — я навіть гадки не мала, наскільки далеко сягає брехня Оази!"

    "Від усвідомлення мене починає нудити. Дихання перехоплює, в горлі стає ком"

    "Годіум — це люди!"

    show павка сумна at left2
    "Наше штучне щастя — це люди. Про існування яких ми навіть не знали"

    "Нас годують людьми"

    павка "Це жахливо. І огидно. Пробач мені… Я не знала"

    show генто сумний at right2
    генто "Ти ні в чому не винна. Ніхто зі споживачів годіуму не винен"

    show генто усміхнений at right2
    show павка нейтральна at left2
    генто "Я ж казав, що правда тобі не сподобається"

    павка "Як… як ви виживаєте?"

    show генто усміхнений at right2
    show павка розгублена at left2
    "Генто сміється. Мені дивно це бачити, з огляду на те, що з нього, як із інших, регулярно викочували усю природню радість"

    "Дивно і трохи моторошно"

    "Але виглядав він цілком спокійно і навіть енергійно. Якщо не зважати на блідість і худорлявість"

    генто "А як ти гадаєш? Як можуть виживати такі, як я?"

    menu:
        "Антарес контрабандою провозить вам годіум?":
            show генто невдоволений at right2
            show павка нейтральна at left2
            генто "Ну що за дурниці. Звісно ні"

            генто "Ми і є Антарес. В нас є агенти, що живуть на поверхні. Але більшість з нас — тут"
            show генто усміхнений at right2
            генто "У нас немає потреби в годіумі. Як бачиш, він пливе знизу наверх, а не навпаки"

        "Ви вживаєте якийсь інший наркотик?":
            show павка нейтральна at left2
            show генто усміхнений at right2
            генто "Сестро, хіба тобі так важко уявити суспільство, що не складається поголовно зі втичерів та самогубців?"

            show генто невдоволений at right2
            генто "Те, що ви називаєте годіумом — лише знаряддя поневолення в руках Оази"

            генто "В ньому немає потреби там, куди не сягає її влада"

        "Ви навчилися обходитися без речовин?":
            show павка нейтральна at left2
            show генто усміхнений at right2
            генто "В яблучко! Завжди приємно поговорити з розумною людиною"

            генто "Ти вже знаєш відповідь. Адже ти сама нещодавно стала тою, кого на поверхні називають “невживанкою”"

    show павка задумлива at left2
    show генто нейтральний at right2
    павка "То всі ви — невживанці?"

    show генто невдоволений at right2
    show павка нейтральна at left2
    генто "Ми тут не вживаємо це слово. З нашої точки зору, ми просто нормальні люди"

    генто "Те, що коїться у вас там, не поверхні — ось це справді щось ненормальне"

    show павка здивована at left2
    павка "То ви всі в Антаресі? Всі, хто тут живуть?"

    show генто усміхнений at right2
    генто "Сестро, якби це було так, Оаза давно би нас знищила. Ми ретельно приховуємо інформацію про Антарес. І увійти до нього складно…"

    show павка задумлива at left2
    павка "І ви хочете зупинити видачу годіуму до Куполу? Але ж тоді люди помруть від ломки…"

    show генто усміхнений at right2
    show павка нейтральна at left2
    генто "Не помруть. Люди можуть жити без годіуму, це їх природній стан"

    генто "Хіба ти сама вже це не зрозуміла?"

    show павка задумлива at left2
    павка "Так, але…"

    "До мене прийшов спогад. Поки ми йшли лабіринтом тунелей, я прокручувала його в своїй голові"

    stop music
    scene фон
    "..."
    jump scene_twelve
