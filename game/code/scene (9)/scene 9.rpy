
label scene_nine:

    scene фон
    play music "audio/Сцена 3/ПавкаЛайя.mp3" fadeout 3
    "..."

    "{i}Мотузка міцно стискала мої руки. Хтось надавив мені на плечи і змусив сісти"

    play sound "audio/Сцена 3/Шурхіт.mp3"
    "{i}Хтось знімає мішок з моєї голови..."


    scene кімната_лайя
    show павка рішуча at left2 with dissolve
    show лайя нейтральний at right2 with dissolve

    stop sound fadeout 2
    show павка здивована at left2
    павка "Ти!"

    show павка зла at left2
    павка "Ти сказав, що ви просто збираєте інформацію"

    павка "Ти сказав, що операція цілком безпечна. І ти мене обдурив!"

    show лайя рішучий at right2
    лайя "Так, я тебе обдурив. Але я робив те, що мав зробити"

    menu:
        "Я ненавиджу тебе!":
            show павка зла at left2
            show лайя нейтральний at right2
            павка "Ніхто ніколи не робив мені так погано, як ти"

            show лайя усміхнений at right2
            лайя "Хіба що правителі Оази і засновники Купола"

            павка "Ти знущаєшся з мене, чи що?"

        "Це не чесно":
            show павка рішуча at left2
            show лайя нейтральний at right2
            $відносини_лайя= відносини_лайя+1
            павка "Ваша організація за щось там бореться, так?"
            show лайя сумний at right2

            павка "За спрведливість, здається. Хочете досягти чесного суспільства. А самі поводитеся як наші правителі. Ви огидні"

    show павка рішуча at left2
    show лайя нейтральний at right2
    павка "Негайно розв'яжи мене!"

    show лайя рішучий at right2
    лайя "І що тоді? Що ти будеш робити?"

    павка "Тоді ти виконаєш свою частину угоди. Допоможеш мені покинути Купол"

    лайя "З чого ти взяла, що я хочу це робити?"

    menu:
        "Бо ти врятував мене":
            $відносини_лайя= відносини_лайя+1
            show лайя усміхнений at right2
            лайя "Ти дійсно така, якою я собі тебе уявляв, Павко. Ти розумна. Ти здатна пізнати справжню суть речей, побачити невидимі, незаконні зв'язки, які охоплюють всі речі довкола"

        "Тому що я примушу тебе":
            show лайя усміхнений at right2
            лайя "Он як! І як ти збираєшся це зробити?"

            menu:
                "Донесу на тебе":
                    $відносини_лайя= відносини_лайя-1
                    show лайя сумний at right2
                    лайя "Око за око? Ціною другого ока? Ти не здатна на таке. Хоча…"

                "Я не піду звідси, доки ти мені не допоможеш":
                    show лайя здивований at right2
                    лайя "Ти дуже напориста, Павко"

    show лайя нейтральний at right2
    лайя "Але насправді я дійсно збираюся тебе звільнити. Насправді я вже більше не сумніваюсь в тобі"

    павка "То нащо ти мене зв'язав?"

    show лайя усміхнений at right2
    лайя "Хто зна. Можливо, я тебе боюся. Або це просто моя маленька слабкість повністю контролювати ситуацію"

    show лайя рішучий at right2
    лайя "Я звільню тебе. Але спершу я хочу дещо тобі сказати, Павко"

    лайя "Зараз ми перебуваємо в моїй квартирі. Я ризикую усім, намагаючись тобі допомогти, йду наперекір закону Оази і правил організації Антарес"

    лайя "І якщо бодай одна жива душа дізнається, що ти була тут — мені кінець"

    play sound "audio/Сцена 3/Шурхіт.mp3"

    show лайя нейтральний at right2
    show павка нейтральна at left2
    "І він звільнив мене"

    "Я одразу відчула себе спокійніше. Хоча не те, щоб до цього було дуже страшно"
    stop sound fadeout 3
    show лайя усміхнений at right2
    лайя "Оскільки годіум ти не вживаєш, можу запропонувати тобі чаю"

    show павка усміхнена at left2
    павка "Не відмовлюся"

    show павка нейтральна at left2
    show лайя нейтральний at right2
    лайя "2314-й, чай на двох"

    "За хвилину невеличкий столик на колесиках привіз заварник і дві чашки"

    "Зазвичай чай пахне синтетикою. Штучні складові дають термоядерні смаки та запахи"

    "Але цей чаайй… Чи не так пахли квіти з моїх снів?"

    show павка задумлива at left2
    павка "Це великі білі квітки, які ростуть на дереві з багатьма тонкими стовбурами трохи вище за людину?"

    show лайя здивований at right2
    лайя "Звідки ти знаєш про жасмін?"

    show лайя нейтральний at right2
    show павка замріяна at left2
    павка "Жасмін… Яке красиве слово"

    show павка нейтральна at left2
    "Лайя дивився мені просто в вічі, чекаючи відповіді на своє запитання"

    show павка рішуча at left2
    павка "Відповім, якщо розповіси мені, що відбувається"

    show павка розгублена at left2
    павка "Що я сьогодні насправді робила? І що тепер буде з містом?"

    show лайя рішучий at right2
    лайя "Ти допомогла нам передати Оазі повідомлення. Це початок війни"

    show павка задумлива at left2
    павка "Ви — це Антарес? Що ви хочете зробити з містом?"

    лайя "Зараз у місті хаос, населення шоковане, але деякі переходять до збройного спротиву"

    show лайя обурений at right2
    лайя "Ми знищимо годіумну імперію. Це був перший, але не останній удар. На її руїнах буде побудоване справедливе суспільство"

    show лайя рішучий at right2
    show павка розгублена at left2
    павка "Це все дуже несподіванно. Я думала, ви просто допомагаєте таким, як я"

    show павка задумлива at left2
    лайя "Так і є. Ми допомагаємо всім нам отримати свободу. Хіба це не те, чого ти хотіла?"

    show лайя усміхнений at right2
    лайя "Тепер ти. Розкажи, звідки знаєш про квіти"

    show павка нейтральна at left2
    павка "Квіти? Я бачила їх у своїх снах. Як і багато чого іншого"

    show лайя усміхнений at right2
    лайя "Ти справжня знахідка"

    show лайя рішучий at right2
    лайя "Ти сама придумала музику, хочя ніхто не вчив тебе цьому"

    лайя "Ти особлива. І тому ти маєш виконати місію"

    show павка здивована at left2
    павка "Що?"

    лайя "Ти маєш піти за межі Купола і знайти такі пісні, які допоможуть укріпити справедливу владу у Куполі"

    лайя "Мистецтво — це твоя сила. Твій інструмент впливу на людей. І ти можеш використати її на благо людства"

    show павка рішуча at left2
    "Я просто хотіла втекти з Купола"

    "І ще сьогодні вранці я ледь знайшла в собі сили наважитися на цей крок. Я сподівалася на чесну угоду"

    "Але чомусь це не спрацювало. І тепер я побачила, що світ набагато складніший, ніж мені здавалось"

    "Я мушу врятуватись. Заради музики. Заради себе. Але що мені відповісти Лайї?"

    menu:
        "Сумно, що ти так до мене ставишся" :
            show павка сумна at left2
            show лайя рішучий at right2
            $відносини_лайя= відносини_лайя+1
            павка "Що ти хочеш мене використати знов…"

            павка "Я довірила тобі своє життя"

            menu:
                "Я думала, що ти чесна людина":
                    show лайя сумний at right2
                    show павка нейтральна at left2
                    $покора_лайі= покора_лайі -1
                    лайя "А от я так не думав"

                    лайя "Та після нашої зустрічи в мене з'явилися сумніви в тому, що я вчиняю правильно, обманюючи тебе"

                    лайя "Тому ти зараз тут, а не в лапах Перевіряючих"
                    pass

                "Мені здалося, я щось для тебе значу":
                    show лайя усміхнений at right2
                    show павка нейтральна at left2
                    $відносини_лайя= відносини_лайя+1
                    лайя "Тобі не здалося"

                    show лайя сумний at right2

                    лайя "Так, я дав тобі це завдання, але мене мучили сумніви"

                    лайя "Тому ти зараз тут зі мною, а не в лапах Перевіряючих"
                    pass

        "Знаєш, в нас спільна мета…":
            $покора_лайі= покора_лайі+1
            menu:
                "І частина мене хоче йти за тобою":
                    show лайя рішучий at right2
                    $покора_лайі= покора_лайі+1

                    show павка рішуча at left2
                    павка "Я завжди мріяла іти за сильною людиною, як ти. Змінювати світ разом"

                    show павка зла at left2
                    павка "Але ти обдурив і зрадив мене"

                    show лайя сумний at right2
                    лайя "Я просто виконував наказ"

                    лайя "Але тепер я не впевнений, що це був правильний наказ"

                    лайя "Власне, тому ти зараз тут. А не в лапах Перевіряючих"


                "Але я не буду твої пішаком":
                    show лайя рішучий at right2
                    $покора_лайі= покора_лайі -1
                    show павка рішуча at left2
                    павка "Я думала, що у таких ідеалістів як ти має бути совість"

                    show павка сумна at left2
                    павка "Але тепер бачу, що ти здатен пожертвувати будь-чим заради своїх цілей"

                    show павка зла at left2
                    павка "Я бачу твої маніпуляції наскрізь"

                    show лайя сумний at right2
                    лайя "Ти права лише на половину. Я був таким, як ти кажеш. Таким мене зробив Антарес"

                    лайя "Але після зустрічи з тобою в мене з’явились сумніви"

                    лайя "Власне, тому ти зараз тут. А не в лапах Перевіряючих"

    show лайя нейтральний at right2
    show павка здивована at left2
    павка "Ну і як же змінилося твоє ставлення? Просвіти мене"

    if відносини_лайя < 3 :
        if покора_лайі >= 2:
                show павка нейтральна at left2
                show лайя рішучий at right2
                лайя "Ти заслуговуєш бути чимось більшим ніж простим пішаком"

                лайя "Мистецтво — це зброя. Ти вмієш створювати пісні. Ти — пророк майбутнього"

                лайя "Сьогодні ти побачиш, який насправді великий світ. Він приховує в собі безліч таємниць. Безліч пісень, що створювали Древні"

                лайя "Ти знайдеш їх і принесеш сюди, щоб допомогти нам змінити світ. Ти згодна, Павко?"

                show лайя нейтральний at right2
                лайя "Що б ти не відповіла, я допоможу тобі. Так, план твого порятунку провалився, але я підготував план Б"

                menu:
                    "Погодитися":
                        $покора_лайі= покора_лайі+1
                        лайя "Чудово. Радий, що ти обрала співпрацювати зі мною"

                        лайя "Повір, ти зробили правильний вибір. З тобою під моїм керівництвом ми зможемо побудувати краще майбутне"

                        лайя "Щодо твого порятунку, то ось мій план Б"


                    "Відмовитися":
                        лайя "Що ж…"

                        лайя "Я сподівався, що ти мені допоможеш. Але ти не та, за кого я тебе приймав"

                        лайя "Але ми домовилися, і я стримаю обіцянку. Так, план твого порятунку провалився, але я підготував план Б"

                        лайя "Тобі допоможуть наші люди. І ти врятуєшся. Але з цього моменту забудь моє ім'я"

        if покора_лайі < 2:
            show павка нейтральна at left2
            show лайя рішучий at right2
            лайя "Я подумав, що зможу попросити тебе допомогти мені з чимось"

            show лайя сумний at right2
            лайя "Але тепер бачу, що ти не налаштована на співпрацю. Прикро"

            show лайя нейтральний at right2
            лайя "В будь якому разі, ми домовилися, і я стримаю обіцянку. Так, план твого порятунку провалився, але я підготував план Б"

            show лайя рішучий at right2
            лайя "Тобі допоможуть наші люди. І ти врятуєшся. Але з цього моменту забудь моє ім'я"

    if відносини_лайя >= 3:
        show лайя рішучий at right2
        show павка нейтральна at left2
        лайя "Антарес зробили мене тим, хто я є. І я пишаюсь цим. Але з їхніми методами я більше не згоден"

        лайя "Вони не мають поводитися з людьми так само, як Оаза, якщо хочуть її перемогти"

        if покора_лайі >=2:
            show лайя рішучий at right2
            show павка нейтральна at left2
            лайя "Павко, весь цей час ти вела себе саме так, як було треба. Ти довела, що вмієш виконувати накази"

            show павка здивована at left2
            лайя "То ж я пропоную тобі стати моєю правою рукою"

            лайя "Мистецтво — це зброя. Ти вмієш створювати пісні. Ти — пророк майбутнього"

            show лайя усміхнений at right2
            лайя "Сьогодні ти побачиш, який насправді великий світ. Він приховує в собі безліч таємниць. Безліч пісень, що створювал ДревніТи переживеш шок. І тоді появляться нові пісні"

            лайя "Ти знайдеш їх і принесеш їх сюди, щоб допомогти  і допоможеш нам змінити світ"

            show лайя рішучий at right2
            лайя "Разом ми побудуємо новий світ"

            menu:
                "Погодитися":
                    show лайя усміхнений at right2
                    $покора_лайі= покора_лайі+1
                    show павка рішуча at left2
                    лайя "Я знав. Бачив це у твоїх очах від самого початку"

                    лайя "Хоча план твого порятунку провалився, я підготував план Б"

                "Відмовитися":
                    show лайя нейтральний at right2
                    show павка рішуча at left2
                    павка "Пам'ятаєш, вранці ти казав, що в мене є свої цілі і що це добре?"

                    павка "Так от. Ти був правий. І так, я вперта. То ти допоможеш мені, чи ні?"

                    show лайя сумний at right2

                    лайя "Так, допоможу. В мене є запасний план порятунку. Але я розраховував на більше"

                    лайя "Рятуйся. Можливо, колись ти ще передумаєш"


        if покора_лайі < 2 :
            show павка нейтральна at left2
            show лайя рішучий at right2
            лайя "В мене були плани на тебе, я хотів переконати тебе принести до Купола пісні, які допоможуть досягти справедливості. І допомогти врятувати жителів Купола мистецтвом"

            show лайя сумний at right2
            лайя "Але я вічдуваю, що це неправильно. Ти маєш сама обирати свій шлях"

            menu:
                "До справедливості разом":
                    show лайя усміхнений at right2
                    show павка рішуча at left2
                    лайя "Ти не можеш собі уявити, наскільки я радий, що ти погодилась"

                    лайя "Я впевнений, разом ми зможемо змінити світ на краще"

                    show лайя рішучий at right2
                    лайя "Але все це потім. Зараз час вивезти тебе з міста!"

                "В мене свій власний шлях":
                    show лайя сумний at right2
                    show павка рішуча at left2
                    лайя "Це твій вибір. В будь якому разі, я допоможу тобі"
                    show лайя рішучий at right2

                    лайя "Так, план твого порятунку провалився, але я підготував план Б"
                    $ renpy.notify('Gained achievement: "Свій шлях"')

    show лайя рішучий at right2
    show павка нейтральна at left2
    лайя "З Купола є три типу виходів: по землі — для дослідників із Альфа-колегіуму, у самій вершині залізного купола, що оточує місто — тим виходом користуються найбагатші жителі Оази, коли летають на сафарі"

    show лайя нейтральний at right2
    лайя "І під землею — виходи зі старих катакомб"

    лайя "Ті катакомби побудували дуже давно, ще до вибуху Сеамула. Ними користуються деякі типи вантажівок, ну і члени нашої органазації, коли це потрібно"

    лайя "Наша людина відвезе тебе до катакомб, і провезе ними аж до самого виходу. Ви зустрінетеся ось тут"

    "Лайя увімкнув голографічну карту міста і ткнув пальцем у трикутничок скверу"

    лайя "Це зовсім близько"

    show павка задумлива at left2

    "Чорт, в нього справді є план. Здається, цього разу на нього можна покластись"

    "Здається, це все?"

    show павка розгублена at left2
    show лайя сумний at right2
    павка "Що ж, тоді я вирушаю…"

    if відносини_лайя >=4:

        show павка нейтральна at left2
        лайя "Зажди…"

        show павка здивована at left2
        павка "Що? Ще в тебе для мене чергове завдання?"

        show лайя усміхнений at right2
        лайя "Ні, нічого такого. Просто подарунок"

        show лайя нейтральний at right2
        show павка нейтральна at left2
        $ renpy.notify('Gained achievement: "Подарунок із мого сну"')
        "Він виймає з-під поли свого плаща маленьку довгасту річ"

        show павка задумлива at left2
        "Дивно, але вона здається мені знайомою… Наче якийсь далекий спогад…"

        "Невже це…"

        show павка здивована at left2
        павка "Це річ, яка робить музику! Я бачила таку саму уві сні"

        show лайя усміхнений at right2
        лайя "Ти не перестаєш мене дивувати. Так, це справді музикальний інструмент — річ, за допомогою якої Древні створювали музику"

        лайя "Я знайшов її у своїх подорожах за Купол. Спробував навчитись грати на ній, але в мене нічого не вийшло"

        show лайя нейтральний at right2
        лайя "Я подумав, що, можливо, вона стане тобі в більшій нагоді"

        павка "Це просто… неймовірно"

        menu:
            "Подякувати Лайю":
                show павка усміхнена at left2
                show лайя нейтральний at right2
                павка "Дякую, я ніколи про таке навіть не мріяла!"

                show лайя усміхнений at right2
                лайя "Це я повинен дякувати тобі. Сподіваюсь, коли небудь ти навчиш мене на ньому грати :)"

                "Що це я бачу на його обличчі? Справжню посмішку?"

            "Обійняти його":
                show лайя нейтральний at right2
                show павка замріяна at left2
                "Я нічого не кажу у відповідь і просто обіймаю Лайю"

                show лайя здивований at right2
                "Декілька секунд він стоїть розгублений, розставивши руки в різні сторони. Але потім відповідає на мої обійми"

                "Ми стоїмо так хвилину чи більше. Я не рахую час. Від його одягу приємно пахне жасміном. Як же не хочется розставатися з цим запахом!"

                "Але все колись закінчується"
    show лайя усміхнений at right2
    show павка усміхнена at left2
    лайя "Прощавай, Павко! Хай ти знайдеш те, чого шукаєш"

    павка "Прощавай, Лайо!"

    scene фон with dissolve
    "..."
    jump scene_ten
