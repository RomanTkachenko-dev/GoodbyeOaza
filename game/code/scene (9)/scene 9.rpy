
label scene_nine:

    scene фон
    play music "audio/Сцена 3/ПавкаЛайя.mp3" fadeout 3
    "..."

    "{i}Мотузка міцно стискала мої руки. Хтось надавив мені на плечи і змусив сісти."

    play sound "audio/Сцена 3/Шурхіт.mp3"
    "{i}Хтось знімає мішок з моєї голови..."


    scene кімната_лайя
    show павка рішуча at left2 with dissolve
    show лайя нейтральний at right2 with dissolve

    stop sound fadeout 2
    show павка здивована at left2 with dissolve
    павка "Ти!"

    show павка зла at left2 with dissolve
    павка "Ти сказав, що ви просто збираєте інформацію."

    павка "Ти сказав, що операція цілком безпечна. І ти мене обдурив!"

    show лайя рішучий at right2 with dissolve
    лайя "Так, я тебе обдурив. Але я робив те, що мав зробити."

    menu:
        "Я ненавиджу тебе!":
            show павка зла at left2 with dissolve
            павка "Ніхто ніколи не робив мені так погано, як ти."

            show лайя усміхнений at right2 with dissolve
            лайя "Хіба що правителі Оази і засновники Купола."

            павка "Ти знущаєшся з мене, чи що?"

        "Це не чесно":
            show павка рішуча at left2 with dissolve
            $відносини_лайя += 1
            павка "Ваша організація за щось там бореться, так?"

            show лайя сумний at right2 with dissolve
            павка "За спрведливість, здається. Хочете досягти чесного суспільства. А самі поводитеся як наші правителі. Ви огидні."

    show павка рішуча at left2 with dissolve
    павка "Негайно розв'яжи мене!"

    show лайя рішучий at right2 with dissolve
    лайя "І що тоді? Що ти будеш робити?"

    павка "Тоді ти виконаєш свою частину угоди. Допоможеш мені покинути Купол."

    лайя "З чого ти взяла, що я хочу це робити?"

    menu:
        "Бо ти врятував мене":
            $відносини_лайя += 1
            show лайя усміхнений at right2 with dissolve
            лайя "Ти дійсно така, якою я собі тебе уявляв, Павко. Ти розумна. Ти здатна пізнати справжню суть речей, побачити невидимі, незаконні зв'язки, які охоплюють всі речі довкола."

        "Тому що я примушу тебе":
            show лайя усміхнений at right2 with dissolve
            лайя "Он як! І як ти збираєшся це зробити?"

            menu:
                "Донесу на тебе":
                    $відносини_лайя -= 1
                    show лайя сумний at right2 with dissolve
                    лайя "Око за око? Ціною другого ока? Ти не здатна на таке. Хоча…"

                "Я не піду звідси, доки ти мені не допоможеш":
                    show лайя здивований at right2 with dissolve
                    лайя "Ти дуже напориста, Павко."

    show лайя нейтральний at right2 with dissolve
    лайя "Але насправді я дійсно збираюся тебе звільнити. Насправді я вже більше не сумніваюсь в тобі."

    павка "То нащо ти мене зв'язав?"

    show лайя усміхнений at right2 with dissolve
    лайя "Хто зна. Можливо, я тебе боюся. Або це просто моя маленька слабкість повністю контролювати ситуацію."

    show лайя рішучий at right2 with dissolve
    лайя "Я звільню тебе. Але спершу я хочу дещо тобі сказати, Павко."

    лайя "Зараз ми перебуваємо в моїй квартирі. Я ризикую усім, намагаючись тобі допомогти, йду наперекір закону Оази і правил організації Антарес."

    лайя "І якщо бодай одна жива душа дізнається, що ти була тут — мені кінець."

    play sound "audio/Сцена 3/Шурхіт.mp3"

    show павка нейтральна at left2
    "Він знімає наручники."

    "Одразу стає спокійніше. Хоча не те, щоб до цього було дуже страшно."

    stop sound fadeout 3
    show лайя усміхнений at right2 with dissolve
    лайя "Оскільки годіум ти не вживаєш, можу запропонувати тобі чаю."

    show павка усміхнена at left2 with dissolve
    павка "Не відмовлюся."

    show лайя нейтральний at right2 with dissolve
    лайя "2314-й, чай на двох."

    "За хвилину невеличкий столик на колесиках привіз заварник і дві чашки."

    "Зазвичай чай пахне синтетикою. Штучні складові дають термоядерні смаки та запахи."

    "Але цей чаайй… Чи не так пахли квіти з моїх снів?"

    show павка задумлива at left2 with dissolve
    павка "Це великі білі квітки, які ростуть на дереві з багатьма тонкими стовбурами трохи вище за людину?"

    show лайя здивований at right2 with dissolve
    лайя "Звідки ти знаєш про жасмін?"

    show павка замріяна at left2 with dissolve
    павка "Жасмін… Яке красиве слово."

    show павка нейтральна at left2 with dissolve
    "Лайя дивився мені просто в вічі, чекаючи відповіді на своє запитання."

    show павка рішуча at left2 with dissolve
    павка "Відповім, якщо розповіси мені, що відбувається."

    show павка розгублена at left2 with dissolve
    павка "Що я сьогодні насправді робила? І що тепер буде з містом?"

    show лайя рішучий at right2 with dissolve
    лайя "Ти допомогла нам передати Оазі повідомлення. Це початок війни."

    show павка задумлива at left2 with dissolve
    павка "Ви — це Антарес? Що ви хочете зробити з містом?"

    лайя "Зараз у місті хаос, населення шоковане, але деякі переходять до збройного спротиву."

    show лайя обурений at right2 with dissolve
    лайя "Ми знищимо годіумну імперію. Це був перший, але не останній удар. На її руїнах буде побудоване справедливе суспільство."

    show павка розгублена at left2 with dissolve
    павка "Це все дуже несподіванно. Я думала, ви просто допомагаєте таким, як я."

    лайя "Так і є. Ми допомагаємо всім нам отримати свободу. Хіба це не те, чого ти хотіла?"

    show лайя усміхнений at right2 with dissolve
    лайя "Тепер ти. Розкажи, звідки знаєш про квіти."

    show павка нейтральна at left2 with dissolve
    павка "Квіти? Я бачила їх у своїх снах. Як і багато чого іншого."

    show лайя усміхнений at right2 with dissolve
    лайя "Ти справжня знахідка."

    show лайя рішучий at right2 with dissolve
    лайя "Ти сама придумала музику, хочя ніхто не вчив тебе цьому."

    лайя "Ти особлива. І тому ти маєш виконати місію."

    show павка здивована at left2 with dissolve
    павка "Що?"

    лайя "Ти маєш піти за межі Купола і знайти такі пісні, які допоможуть укріпити справедливу владу у Куполі."

    лайя "Мистецтво — це твоя сила. Твій інструмент впливу на людей. І ти можеш використати її на благо людства."

    show павка рішуча at left2 with dissolve
    "{i}Я просто хотіла втекти з Купола."

    "{i}І ще сьогодні вранці я ледь знайшла в собі сили наважитися на цей крок. Я сподівалася на чесну угоду."

    "{i}Але чомусь це не спрацювало. І тепер я побачила, що світ набагато складніший, ніж мені здавалось."

    "{i}Я мушу врятуватись. Заради музики. Заради себе. Але що мені відповісти Лайї?"

    menu:
        "Сумно, що ти так до мене ставишся" :
            show павка сумна at left2 with dissolve
            $відносини_лайя += 1
            павка "Що ти хочеш мене використати знов…"

            павка "Я довірила тобі своє життя."

            menu:
                "Я думала, що ти чесна людина":
                    show лайя сумний at right2 with dissolve
                    $покора_лайі -= 1
                    лайя "А от я так не думав."

                    лайя "Та після нашої зустрічи в мене з'явилися сумніви в тому, що я вчиняю правильно, обманюючи тебе."

                    лайя "Тому ти зараз тут, а не в лапах перевіряючих."
                    pass

                "Мені здалося, я щось для тебе значу":
                    show лайя усміхнений at right2 with dissolve
                    $відносини_лайя += 1
                    лайя "Тобі не здалося."

                    show лайя сумний at right2 with dissolve
                    лайя "Так, я дав тобі це завдання, але мене мучили сумніви."

                    лайя "Тому ти зараз тут зі мною, а не в лапах Перевіряючих."
                    pass

        "Знаєш, в нас спільна мета…":
            $покора_лайі += 1
            menu:
                "І частина мене хоче йти за тобою":
                    $покора_лайі += 1
                    show павка рішуча at left2 with dissolve
                    павка "Я завжди мріяла іти за сильною людиною, як ти. Змінювати світ разом."

                    show павка зла at left2 with dissolve
                    павка "Але ти обдурив і зрадив мене."

                    show лайя сумний at right2 with dissolve
                    лайя "Я просто виконував наказ."

                    лайя "Але тепер я не впевнений, що це був правильний наказ."

                    лайя "Власне, тому ти зараз тут. А не в лапах перевіряючих."

                "Але я не буду твої пішаком":
                    $покора_лайі -= 1
                    show павка рішуча at left2 with dissolve
                    павка "Я думала, що у таких ідеалістів як ти має бути совість."

                    show павка сумна at left2 with dissolve
                    павка "Але тепер бачу, що ти здатен пожертвувати будь-чим заради своїх цілей."

                    show павка зла at left2 with dissolve
                    павка "Я бачу твої маніпуляції наскрізь."

                    show лайя сумний at right2 with dissolve
                    лайя "Ти права лише на половину. Я був таким, як ти кажеш. Таким мене зробив Антарес."

                    лайя "Але після зустрічи з тобою в мене з’явились сумніви."

                    лайя "Власне, тому ти зараз тут. А не в лапах Перевіряючих."

    show павка здивована at left2 with dissolve
    павка "Ну і як же змінилося твоє ставлення? Просвіти мене."

    if відносини_лайя < 3 :
        if покора_лайі >= 2:
                show лайя рішучий at right2 with dissolve
                лайя "Ти заслуговуєш бути чимось більшим ніж простим пішаком."

                лайя "Мистецтво — це зброя. Ти вмієш створювати пісні. Ти — пророк майбутнього."

                лайя "Сьогодні ти побачиш, який насправді великий світ. Він приховує в собі безліч таємниць. Безліч пісень, що створювали Древні."

                лайя "Ти знайдеш їх і принесеш сюди, щоб допомогти нам змінити світ. Ти згодна, Павко?"

                show лайя нейтральний at right2 with dissolve
                лайя "Що б ти не відповіла, я допоможу тобі. Так, план твого порятунку провалився, але я підготував план Б."

                menu:
                    "Погодитися":
                        $покора_лайі += 1
                        лайя "Чудово. Радий, що ти обрала співпрацювати зі мною."

                        лайя "Повір, ти зробили правильний вибір. З тобою під моїм керівництвом ми зможемо побудувати краще майбутне."

                        лайя "Щодо твого порятунку, то ось мій план Б."

                    "Відмовитися":
                        лайя "Що ж…"

                        лайя "Я сподівався, що ти мені допоможеш. Але ти не та, за кого я тебе приймав."

                        лайя "Але ми домовилися, і я стримаю обіцянку. Так, план твого порятунку провалився, але я підготував план Б."

                        лайя "Тобі допоможуть наші люди. І ти врятуєшся. Але з цього моменту забудь моє ім'я."

        if покора_лайі < 2:
            show лайя рішучий at right2 with dissolve
            лайя "Я подумав, що зможу попросити тебе допомогти мені з чимось."
я
            show лайя сумний at right2 with dissolve
            лайя "Але тепер бачу, що ти не налаштована на співпрацю. Прикро."

            show лайя нейтральний at right2 with dissolve
            лайя "В будь-якому разі, ми домовилися, і я стримаю обіцянку. Так, план твого порятунку провалився, але я підготував план Б."

            show лайя рішучий at right2 with dissolve
            лайя "Тобі допоможуть наші люди. І ти врятуєшся. Але з цього моменту забудь моє ім'я."

    if відносини_лайя >= 3:
        show лайя рішучий at right2 with dissolve
        лайя "Антарес зробили мене тим, хто я є. І я пишаюсь цим. Але з їхніми методами я більше не згоден."

        лайя "Вони не мають поводитися з людьми так само, як Оаза, якщо хочуть її перемогти."

        if покора_лайі >=2:
            show лайя рішучий at right2 with dissolve
            лайя "Павко, весь цей час ти вела себе саме так, як було треба. Ти довела, що вмієш виконувати накази."

            show павка здивована at left2 with dissolve
            лайя "То ж я пропоную тобі стати моєю правою рукою."

            лайя "Мистецтво — це зброя. Ти вмієш створювати пісні. Ти — пророк майбутнього."

            show лайя усміхнений at right2 with dissolve
            лайя "Сьогодні ти побачиш, який насправді великий світ. Він приховує в собі безліч таємниць. Безліч пісень, що створювал ДревніТи переживеш шок. І тоді появляться нові пісні."

            лайя "Ти знайдеш їх і принесеш їх сюди, щоб допомогти  і допоможеш нам змінити світ."

            show лайя рішучий at right2 with dissolve
            лайя "Разом ми побудуємо новий світ."

            menu:
                "Погодитися":
                    show лайя усміхнений at right2 with dissolve
                    $покора_лайі += 1
                    лайя "Я знав. Бачив це у твоїх очах від самого початку."

                    лайя "Хоча план твого порятунку провалився, я підготував план Б."

                "Відмовитися":
                    show павка рішуча at left2 with dissolve
                    павка "Пам'ятаєш, вранці ти казав, що в мене є свої цілі і що це добре?"

                    павка "Так от. Ти був правий. І так, я вперта. То ти допоможеш мені, чи ні?"

                    show лайя сумний at right2 with dissolve
                    лайя "Так, допоможу. В мене є запасний план порятунку. Але я розраховував на більше."

                    лайя "Рятуйся. Можливо, колись ти ще передумаєш."

        if покора_лайі < 2 :
            show лайя рішучий at right2 with dissolve
            лайя "В мене були плани на тебе, я хотів переконати тебе принести до Купола пісні, які допоможуть досягти справедливості. І допомогти врятувати жителів Купола мистецтвом."

            show лайя сумний at right2 with dissolve
            лайя "Але я вічдуваю, що це неправильно. Ти маєш сама обирати свій шлях."

            menu:
                "До справедливості разом":
                    show лайя усміхнений at right2 with dissolve
                    лайя "Ти не можеш собі уявити, наскільки я радий, що ти погодилась."

                    лайя "Я впевнений, разом ми зможемо змінити світ на краще."

                    show лайя рішучий at right2 with dissolve
                    лайя "Але все це потім. Зараз час вивезти тебе з міста!"

                "В мене свій власний шлях":
                    $ renpy.notify('Gained achievement: "Свій шлях"')
                    show лайя сумний at right2 with dissolve
                    лайя "Це твій вибір. В будь якому разі, я допоможу тобі."

                    show лайя рішучий at right2 with dissolve
                    лайя "Так, план твого порятунку провалився, але я підготував план Б."

    show лайя рішучий at right2 with dissolve
    лайя "З Купола є три типу виходів: по землі — для дослідників із Альфа-колегіуму, у самій вершині залізного купола, що оточує місто — тим виходом користуються найбагатші жителі Оази, коли летають на сафарі."

    show лайя нейтральний at right2 with dissolve
    лайя "І під землею — виходи зі старих катакомб."

    лайя "Ті катакомби побудували дуже давно, ще до вибуху Сеамула. Ними користуються деякі типи вантажівок, ну і члени нашої органазації, коли це потрібно."

    лайя "Наша людина відвезе тебе до катакомб, і провезе ними аж до самого виходу. Ви зустрінетеся ось тут."

    "{i}Лайя увімкнув голографічну карту міста і ткнув пальцем у трикутничок скверу."

    лайя "Це зовсім близько."

    show павка задумлива at left2 with dissolve
    "{i}Чорт, в нього справді є план. Здається, цього разу на нього можна покластись."

    "{i}Здається, це все?"

    show павка розгублена at left2 with dissolve
    павка "Що ж, тоді я вирушаю…"

    if відносини_лайя >=4:

        show лайя рішучий at right2 with dissolve
        лайя "Зажди…"

        show павка здивована at left2 with dissolve
        павка "Що? Ще в тебе для мене чергове завдання?"

        show лайя усміхнений at right2 with dissolve
        лайя "Ні, нічого такого. Просто подарунок."

        show павка нейтральна at left2 with dissolve
        $ renpy.notify('Gained achievement: "Подарунок із мого сну"')
        "{i}Він виймає з-під поли свого плаща маленьку довгасту річ."

        show павка задумлива at left2
        "{i}Дивно, але вона здається мені знайомою… Наче якийсь далекий спогад…"

        "{i}Невже це…"

        show павка здивована at left2 with dissolve
        павка "Це річ, яка робить музику! Я бачила таку саму уві сні."

        show лайя здивований at right2 with dissolve
        лайя "Ти не перестаєш мене дивувати. Так, це справді музикальний інструмент — річ, за допомогою якої Древні створювали музику."

        лайя "Я знайшов її у своїх подорожах за Купол. Спробував навчитись грати на ній, але в мене нічого не вийшло."

        show лайя нейтральний at right2 with dissolve
        лайя "Я подумав, що, можливо, вона стане тобі в більшій нагоді."

        павка "Це просто… неймовірно."

        menu:
            "Подякувати Лайю":
                show павка усміхнена at left2 with dissolve
                павка "Дякую, я ніколи про таке навіть не мріяла!"

                show лайя усміхнений at right2 with dissolve
                лайя "Це я повинен дякувати тобі. Сподіваюсь, коли небудь ти навчиш мене на ньому грати :)"

                "{i}Що це я бачу на його обличчі? Справжню посмішку?"

            "Обійняти його":
                show павка замріяна at left2 with dissolve
                "{i}Я нічого не кажу у відповідь і просто обіймаю Лайю."

                show лайя здивований at right2 with dissolve
                "{i}Декілька секунд він стоїть розгублений, розставивши руки в різні сторони. Але потім відповідає на мої обійми."

                "{i}Ми стоїмо так хвилину чи більше. Я не рахую час. Від його одягу приємно пахне жасміном. Як же не хочется розставатися з цим запахом!"

                "{i}Але все колись закінчується."

    show лайя усміхнений at right2 with dissolve
    лайя "Прощавай, Павко! Хай ти знайдеш те, чого шукаєш."

    show павка весела at left2 with dissolve
    павка "Прощавай, Лайо!"

    scene фон with dissolve
    "..."
    jump scene_ten
