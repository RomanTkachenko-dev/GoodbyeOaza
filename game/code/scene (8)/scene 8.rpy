
label scene_eight:
    scene погоня with dissolve

    $ time = 7
    $ timer_range = 7
    $ timer_jump = 'proval'
    play music "audio/Сцена 8/Прискорене серцебиття.mp3" fadeout 1
    "{i}Ноги самі несуть мене геть"

    "{i}Кудись подалі від того клятого автомату"

    "{i}Подалі від цього страшного голосу"
    stop music
    play music "audio/Сцена 7/Тривога.mp3" fadeout 1

    гучномовець "ТРИВОГА! ТРИВОГА! РОЗХОДЬТЕСЯ ПО СВОЇХ ДОМІВКАХ І ЧЕКАЙТЕ НА ПЕРЕВІРЯЮЧИХ. ЗАБОРОНЕНО ЗАЛИШАТИ ОАЗУ, ДОКИ ДИВЕРСАНТА НЕ БУДЕ ЗНАЙДЕНО"

    "{i}Але від нього неможливо втекти. Він лунає з кожного кута, з кожної стіни"

    "{i}Час зупинитись і подумати. Доки я не накоїла дурниць"
    stop music
    play music "audio/Сцена 7/ТемаПередВтечею.mp3" fadeout 3
    menu:
        "Зупинитись":
            "{i}Фух. Час видохнути і зібратись з думками"

    #

    "{i}Перевіряючі, напвено, вже дістались до того трансконнектору"

    if відносини_інкі < 2:
        "{i}Інкі, напевно, вже навела їх на мій слід"

        "{i}Значить, часу в мене небагато"

    if відносини_інкі >= 2:
        "{i}Сподіваюсь, Інкі зможе їх затримати. Це дасть мені трохи часу"

        "{i}Дякую тобі, подруго. Сподіваюсь, з тобою все буде гаразд"
    #stop music
    #play music "audio/Сцена 8/Тема Втечі.mp3"
    "{i}Що ж мені робити?"

    menu:
        "Лайя!":
            "{i}Точно, Лайя!"

    "{i}Це він втягнув мене у все це! 'Просто збираємо інформацію' - а як же! От негідник!"

    "{i}Але він згадував якісь адреси... Треба пригадати…"

    "{i}Згадала! Він казав про вулицю Чисту, будинок 2, і чоловіка в капелюсі…"

    "{i}А ще про південно-східну браму, біля якої я повинна буду його зустріти"

    "{i}До брами далеко. Але якщо транспортери ще літають, я зможу застрибнути на один з них і доїхати прямо до неї"

    "{i}Вулиця Чиста всього в двох кварталах звідси. Але щось мені підказує, що прямувати пішки буде небезпечніше"

    "{i}Куди мені слід вирушити?"

    menu:
        "Бігти на вулицю":
            $брама = False
            $вулиця = True
            "{i}Хто знає, що може статися зі мною у транспортері?"
            "{i}Пішки в мене більше шансів втекти. До того ж це зовсім недалеко"
        "Прямувати до брами":
            $вулиця = False
            $брама = True
            $ renpy.notify('Gained achievement: "Чоловік у капелюсі"')
            "{i}Лайя повинен зараз бути біля брами. Значить, слід вирушати туди"
            "{i}До того ж, це ближче до виходу з міста"

    play music "audio/Сцена 8/Тема Втечі.mp3"
    if відносини_інкі < 2:
        play sound "audio/Сцена 8/Звук Робота.mp3"
        перевіряючий "Охопити периметр. Обшукати кожен будинок. Злочинець не міг втекти далеко"
        "{i}Дідько! Перевіряючі вже майже тут"
        stop sound fadeout 3


        $ time = 7
        $ timer_range = 7
        show screen countdown
        menu:
            "Бігти не озираючись":
                hide screen countdown
                $ непомітність = непомітність - 1
                $ швидкість = швидкість + 2
                "{i}Не підведіть мене, ніженьки!"
                "{i}Нехай спробують наздогнати!"


    elif відносини_інкі >= 2:
        "{i}Тривога не стихає, але копів ще не чути. В мене є трохи часу для роздумів. Як саме мені краще пересуватись містом?"

        play sound "audio/Сцена 8/Звук Таймера.mp3"

        $ time = 7
        $ timer_range = 7
        show screen countdown
        menu:
            "Бігти не озираючись ":
                hide screen countdown
                stop sound fadeout 3
                $ непомітність = непомітність - 1
                $ швидкість = швидкість + 2
                "{i}Не підведіть мене, ніженьки!"
                "{i}Нехай спробують наздогнати!"
            "Залишатися в тіні":
                hide screen countdown
                stop sound fadeout 3
                $ непомітність = непомітність + 2
                $ швидкість = швидкість - 1
                "{i}Я знаю це місто як свої п'ять пальців"
                "{i}Тут неподалік є завулок, через нього можна прослизнути непоміченою"
            "Спробувати змішатись з натовпом":
                hide screen countdown
                stop sound fadeout 3
                $ непомітність = непомітність + 1
                $ швидкість = швидкість + 1
                "{i}Якщо пристану до от тої групки вживачів, то зможу вислизнути з вулиці непоміченою"
                "{i}Вони настільки обдовбані, що навіть не розуміють, що відбувається"

    play sound "audio/Сцена 8/Звук Робота.mp3"
    перевіряючий "Охопити периметр. Обшукати кожен будинок. Злочинець не міг втекти далеко"
    "Як же вчасно я пішла з вулиці!"
    stop sound fadeout 4

    "{i}Позаду чутно неприємне металеве тріскотіння перевіряючих"
    play sound "audio/Сцена 8/Звук Важкої Металевої Брами.mp3"
    "{i}Буквально за моєю спиною зачиняється велика металева брама"
    "{i}Вулицю перекрито, всі, хто на ній залишилися, будуть ретельно перевірені і проскановані"
    "{i}Я не раз бачила таке. Коли ми викликали копів, щоб затримати вандалів"
    "{i}Але я ніколи ще не ВТІКАЛА від копів!"
    stop sound fadeout 3
    if вулиця == True:
        jump street
    if брама == True:
        jump brama

label street:
    "{i}Чиста вулиця наліво від мене. Три хвилини бігом"
    "{i}Я вже бачу верхній край того клятого бігборду, про який казав Лайя"
    "{i}Він стоїть прямо на мурі, що відділяє Оазу від решти міста"
    "{i}До нього можна спробувати піднятись крутими вуличними сходами, що піднімаються поміж будинків"
    "{i}Або ж видертися на дах комплексу апартаментів. В ньому напевно є ліфт, хоча пересування ним може привернути багато зайвої уваги"
    play music "audio/Сцена 8/Звук Тривога.mp3"
    гучномовець "ТРИВОГА! ТРИВОГА!"

    "{i}Клятий гучномовець продовжує голосити"
    "{i}Дуже скоро цю вулицю теж перекриють. Нема часу на зволікання"
    stop music
    play sound "audio/Сцена 8/Звук Таймера.mp3"

    $ timer_jump = 'proval2'
    $ time = 7
    $ timer_range = 7
    show screen countdown
    menu:
        "Бігти навпростець":
            play music "audio/Сцена 8/Тема Втечі.mp3"
            hide screen countdown
            stop sound fadeout 3
            $ непомітність = непомітність + 1
            $ швидкість = швидкість - 1
            play sound "audio/Сцена 8/Звук Важкої Металевої Брами.mp3"
            "{i}Клац! Ледь встигла. Брама зачинилась прямо за мною"
            "{i}Далі - по сходах наверх, на мур, що відділяє Оазу від периферії"
            "{i}Після підйому в мене ледь вистачає дихання. Це місто явно проектували не для піших прогулянок"

        "Пройти крізь комплекс":
            hide screen countdown
            stop sound fadeout 3
            $ непомітність = непомітність - 1
            $ швидкість = швидкість + 1

            "{i}Дах комплексу стоїть впритул до мура"
            "{i}Зсередини комплекс виглядає як розшурханий мурашник"
            "{i}Мешканці зі здивуванням і острахом зиркають на мене зі своїх осель. Хтось із них, напевно, вже доповідає копам…"
            "{i}Ну що ж, проте хоча б скористаюсь ліфтом!"


    $ timer_jump = 'proval'
    scene погоня_2 with dissolve

    "{i}Нарешті, я на Чистій вулиці"
    "{i}Найдовша вулиця під Куполом. Вона огинає всю Оазу одним великим кільцем"
    "{i}Одне велике, нескінченне кільце. На внутрішній стороні - сотні будинків, що туляться один на одного, наче якісь органічні нарости"
    "{i}Зовні - обрив, що відділяє Оазу від периферії. А тих, хто має все - від тих, хто не має нічого"
    "{i}Але зараз не час для екскурсій"
    "{i}Ось він - бігборд, під яким на мене повинен чекати чоловік в капелюсі"
    "{i}Але…"

    if непомітність >=3 :
        "{i}Жодної людини в капелюсі. Ані чоловіків, ані жінок"
        "{i}Лайя, щоб тобі!"
        "{i}Ніякого чоловіка в капелюсі ніколи не було. Ніхто не чекав на мене, щоб передати перепустку з цього проклятого міста"
        "{i}Мене підставили, використали як пішака, і кинули на призволяще"
        "{i}А я, наївна дурепа, повірила в казки про життя поза Куполом…"

        play music "audio/Сцена 8/Звук Тривога.mp3"

        гучномовець "УВАГА! ВСІ ГРОМАДЯНИ, ЩО ПЕРЕБУВАЮТЬ НА ВУЛИЦІ ПІД ЧАС ЗАГАЛЬНОЇ ТРИВОГИ, ПІДЛЯГАЮТЬ НЕГАЙНІЙ ПЕРЕВІРЦІ! НЕ НАМАГАЙТЕСЯ ЧИНИТИ ОПІР!"
        "{i}Вже ніхто не прийде мене рятувати…"
        "{i}Ось і все, Павко, час прощатися настав"
        stop music fadeout 1
        play sound "audio/Сцена 8/Далекий Вибух.mp3"
        play music "audio/Сцена 8/Тема Втечі.mp3"
        "{i}Що це в дідька ще таке?"
        scene погоня_2 at shaking
        "..."
        scene погоня_2
        "{i}Гучномовець обривається на півслові"
        scene погоня_2 at shaking
        play sound "audio/Сцена 8/Близький Вибух.mp3"
        "{i}За першим вибухом чутно ще один, ближче і гучніше"
        scene погоня_2
        stop sound fadeout 2

        menu:
            "Бігти в укриття":
                "Чорт його знає, де тут безпечне укриття"
                "Машинально забігаю в комплекс апартаментів. Принаймні, тут мене не зачепить уламками"
                jump scene_eight_end
    if непомітність < 3 :
        "{i}На мене вже чекають"
        "{i}Але це не чоловік в капелюсі"

        play sound "audio/Сцена 8/Звук Робота.mp3"

        перевіряючий "Громадянко! Залишайтесь на місці. Ви підозрюєтесь у державній зраді"

        stop sound fadeout 3
        "{i}Клятий робот вже висадився на дах і наближається до мене"
        "{i}Здається, я була недостатньо обережною. Але що робити тепер?"

        play sound "audio/Сцена 8/Звук Таймера.mp3"
        $ time = 7
        $ timer_range = 7
        show screen countdown
        menu:
            "Тікати назад":
                hide screen countdown
                павка "А як же, не дочекаєшся!"
                "{i}Я прослизаю вниз по сходах до того, як ця бляшанка схопить мене по руках і ногах своїми металевими щупальцями"
                play sound "audio/Сцена 8/Далекий Вибух.mp3"
                scene погоня_2 at shaking
                "..."
                scene погоня_2
                "{i}Що це в дідька таке?"
                play sound "audio/Сцена 8/Близький Вибух.mp3"
                scene погоня_2 at shaking
                "{i}За першим вибухом чутно ще один, ближче і гучніше"
                scene погоня_2
                stop sound fadeout 2

                "{i}Сподіваюсь, стіни будинку захистять мене від уламків"

            "Стояти на місці":
                hide screen countdown
                "{i}Ще нікому не вдавалося втекти від Перевіряючого. Ці машини були створені спеціально для того, щоб ефективно ловити втікачів"
                "{i}Але це не означає, що мої справи безнадійні. Треба тільки правильно підібрати момент"
                "{i}Робот наближається впритул до мене. Ще секунда - і він зв'яже мої кінцівки своїми щупальцями"


                play sound "audio/Сцена 8/Звук Таймера.mp3"
                $ timer_jump = 'proval3'
                $ time = 7
                $ timer_range = 7
                show screen countdown
                menu:
                    "Застосувати електрошокер":
                        hide screen countdown
                        play sound "audio/Сцена 8/Звук Електрошокера.mp3"
                        павка "Ось тобі, потворо!"
                        stop sound fadeout 3
                        "{i}Це був останній заряд електрошокеру, але який постріл!"

                        "{i}Робот з декілька разів дригається від потужного заряду, потім з металевим скавучанням падає на землю"
                        "{i}Можливо я і не врятуюсь сьогодні, але до чого ж приємно було вмазати цьому опудалу!"

                        play sound "audio/Сцена 8/Далекий Вибух.mp3"
                        scene погоня_2 at shaking
                        "..."
                        scene погоня_2
                        "{i}Що це в дідька таке?"
                        play sound "audio/Сцена 8/Близький Вибух.mp3"
                        scene погоня_2 at shaking
                        "{i}За першим вибухом чутно ще один, ближче і гучніше"
                        scene погоня_2
                        stop sound fadeout 2
                        menu:
                            "Бігти в укриття":
                                "{i}Чорт його знає, де тут безпечне укриття"
                                "{i}Машинально забігаю в комплекс апартаментів. Принаймні, тут мене не зачепить уламками"
                                jump scene_eight_end


label proval:
    play sound "audio/Сцена 8/Звук робота.mp3"

    перевіряючий "Підозрювана локалізована. Ініціюю процедуру захоплення"
    scene gameover with dissolve
    "Ну от і все. На цьому моя історія закінчується…"
    "..."
    "Чи?..."
    jump scene_eight

label proval2:
    hide screen countdown
    play sound "audio/Сцена 8/Звук Важкої Металевої Брами.mp3"

    "{i}Клац! Брама прямо переді мною зачиняється. Через секунду перевіряючі вийдуть зі своїх капсул"
    #"{i}Схоже, вибору в мене тепер немає - доведеться йти крізь комплекс"

    "Я продовжую стояти, не знаючи, що мені робити…"
    "{i}Доки я зволікаю, металева тварюка перелітає на декілька метрів ближче. Тепер годі й думати про втечу"
    play sound "audio/Сцена 8/Звук робота.mp3"
    перевіряючий "Підозрювану захоплено. Вводжу заспокійливе і починаю процедуру перевірки"
    stop sound fadeout 1
    scene gameover with dissolve
    "Ну от і все. На цьому моя історія закінчується…"
    "..."
    "Чи?..."
    jump scene_eight


label proval3:

    "{i}Доки я зволікаю, металева тварюка перелітає на декілька метрів ближче. Тепер годі і думати про втечу"
    play sound "audio/Сцена 8/Звук робота.mp3"
    перевіряючий "Підозрювану захоплено. Вводжу заспокійливе і починаю процедуру перевірки"
    stop sound fadeout 1
    scene gameover with dissolve
    "Ну от і все. На цьому моя історія закінчується…"
    "..."
    "Чи?..."
    jump scene_eight

label brama:

    "{i}Зупинка транспортерів направо від мене. Біля платформи якраз стоїть потрібний мені бус"

    "{i}Його двері широко відчинені, а на платформі вже нікого немає"
    play sound "audio/Сцена 4/Звук автомобіля.mp3"
    "{i}Якщо покваплюсь, ще встигну застрибнути"

    play sound "audio/Сцена 8/Звук Таймера.mp3"
    $ timer_jump = 'proval_brama'
    $ time = 7
    $ timer_range = 7
    show screen countdown
    menu:
        "Оббігти і роздивитись":
            hide screen countdown
            $ непомітність = непомітність + 1
            $ швидкість = швидкість - 1

            "{i}Зайва обережність не завадить. Хто знає, що чекає на мене всередині?"
            "{i}Швидкий огляд не виявляє жодної небезпеки"
            "{i}Салон заповнений досить щільно, але місця ще є"
            "{i}Пасажири розгублено дивляться по сторонах, намагаючись зрозуміти, що відбувається"
            play music "audio/Сцена 7/Тривога.mp3" fadeout 1
            гучномовець "ТРИВОГА! ТРИВОГА!"
            "{i}Клятий гучномовець продовжує голосити"
            "{i}Дуже скоро цю вулицю теж перекриють. Нема часу на зволікання"
            stop music
            play music "audio/Сцена 8/Тема Втечі.mp3"
            $ timer_jump = 'proval_brama'
            $ time = 7
            $ timer_range = 7
            show screen countdown
            menu:
                "Застрибнути в салон":
                    hide screen countdown
                    "{i}В останню мить переді мною в салон влітає якась тітка у фіолетовому балахоні"
                    "{i}Я спокійно заходжу всередину, намагаючись не привертати уваги"

                    play sound "audio/Сцена 8/Закриття Трамвайних Дверей.mp3"

                    "{i}Двері зачиняються з майже заспокійливим шурхотом"

                    play sound "audio/Сцена 8/Механічний Гуркіт Транспорту, Який Рушив З Місця.mp3"

                    "{i}Транспортер з гуркотом вмикає двигуни і з поштовхом відривається від землі"

        "Бігти прямо у двері":
            hide screen countdown
            $ непомітність = непомітність - 1
            $ швидкість = швидкість + 1
            "Хто б міг подумати, а я вмію справді швидко бігати!"

            play sound "audio/Сцена 8/Звук Зіткнення Двох Людей.mp3"

            пасажирка "Дивись куди лізеш!"
            "{i}Здається, я не одна намагаюсь застрибнути в салон в останню хвилину"
            "{i}В дверях транспортеру я стикаюсь з не по рокам спритною жіночкою. Таке враження, що вона просто матералізувалась в повітрі переді мною"

            play music "audio/Сцена 7/Тривога.mp3" fadeout 1

            гучномовець "ТРИВОГА! ТРИВОГА!"
            "{i}Клятий гучномовець продовжує голосити"
            "{i}Ми намагаємося одночасно пролізти в занадто вузькі двері"
            "{i}В останній момент я хапаюсь за поручень і з силою проштовхую себе і її в салон"
            stop music
            play music "audio/Сцена 8/Тема Втечі.mp3"
            play sound "audio/Сцена 8/Звук Зіткнення Двох Людей.mp3"
            "..."
            play sound "audio/Сцена 8/Закриття Трамвайних Дверей.mp3"

            "{i}Ми обидві падаємо на підлогу. Двері транспортеру з шурхотом зачиняються за нами"
            пасажирка "От дурна периферійка! Понаїхало вас в наш район, навіть транспортером користуватися не вмієте!"
            "{i}Обурена тітка підводиться, отряхує поли свого одягу і непевно крокує до вільного місця"
            "{i}Вона явно сп'яніла від годіуму. Це пояснює підвищену спритність і дивний блиск в очах"
            "{i}А також те, що вона не помітила склянку Г36, яка випала у неї з сумки і закотилась під одне з сидіннь"

            play sound "audio/Сцена 8/Звук Таймера.mp3"
            $ time = 7
            $ timer_range = 7
            show screen countdown
            menu:
                "Непомітно взяти годіум собі":
                    hide screen countdown
                    $ втеча_годіум = True
                    "{i}Я машинально хапаю пляшку і кладу собі в кишеню"
                    "{i}Не те, щоб я хотіла його вживати. Але зважаючи на наявні обставини, така річ може стати в нагоді"
                    play sound "audio/Сцена 8/Механічний Гуркіт Транспорту, Який Рушив З Місця.mp3"
                    "{i}Транспортер з гуркотом вмикає двигуни і з поштовхом відривається від землі"
                    "{i}Там йому і місце. Слава невживанцям!"

                    play sound "audio/Сцена 8/Механічний Гуркіт Транспорту, Який Рушив З Місця.mp3"
                    "{i}Транспортер з гуркотом вмикає двигуни і з поштовхом відривається від землі"
                    "{i}Пляшка провалюється в щелину між сидіннями. Тепер її вже не дістати"
                    "{i}Можливо, воно і на краще"

                "Застосувати електрошокер":
                    hide screen countdown
                    play sound "audio/Сцена 8/Звук Електрошокера.mp3"
                    павка "Ось тобі, потворо!"
                    "{i}Це був останній заряд електрошокеру, але який постріл!"
                    "{i}Робот з декілька разів дригається від потужного заряду, потім з металевим скавучанням падає на землю"
                    "{i}Можливо я і не врятуюсь сьогодні, але до чого ж приємно було вмазати цьому опудалу!"
                    "{i}Пасажири з острахом вжимаються у сидіння. Другий робот наближається, треба бігти!"

                    play sound "audio/Сцена 8/Звук Таймера.mp3"
                    menu:
                        "Вистрибнути у вікно":
                            "{i}Вистрибнувши з вікна, я опиняюсь на даху будинку."
                            "{i}Тут я наче на долоні. Єдиний порятунок - сходи, що ведуть вниз, у будівлю."
                            "{i}Я забігаю в середину і роблю декілька кроків, аж тут..."
                            jump scene_eight_end



    "{i}Поки бус набирає висоту, я займаю сидіння і озираюсь по сторонах"
    "{i}Пасажири виглядають збентежено і розгублено"
    "{i}Цей транспортер, як і будь-який інший, обладнаний мініатюрними трансконнекторами, вмонтованими в спинки сидінь"
    "{i}Щоб втомлені пасажири могли отримати свою дозу дешевого щастя прямо по дорозі з роботи до дому"
    "{i}Але зараз ці приблуди не працюють. Схоже, вірус Антаресу дістався і до них"
    "{i}Що станеться з цим містом без годіуму? Важко уявити"
    "{i}Транспортер повільно (занадто повільно!) летить над містом до південно-східної брами"

    scene втеча_без_машини with dissolve
    "{i}Ми вже майже підлітаємо до брами, коли раптом…"
    play sound "audio/Сцена 8/Близький Вибух.mp3"
    "{i}Гучний вибух, одразу за ним - потужний поштовх"
    "{i}Транспортер хитає з боку в бік, наче билинку на вітру"
    play sound "audio/Сцена 8/Далекий Вибух.mp3"
    "{i}Згодом слідує ще один вибух, цього разу більш далекий"
    "{i}Я визираю у вікно і дивлюсь у напрямку брами"
    "{i}Але не бачу її: вся споруда охоплена полум'ям і димом"
    "{i}От і прилетіли…"
    "{i}Якщо хтось і чекав на мене біля тої брами, то зараз точно вже не чекає"
    "{i}Яка ж я була дурна, що повірила тому Лайї…"
    гучномовець "УВАГА ПАСАЖИРАМ БОРТУ 1766! ВИ ПІДЛЯГАЄТЕ НЕГАЙНІЙ ПЕРЕВІРЦІ. ВАШ ТРАНСПОРТЕР НАПРАВЛЕНО ДО НАЙБЛИЖЧОГО АВАРІЙНОГО ПОСАДКОВОГО МАЙДАНЧИКУ"
    "{i}Дідько! Це пастка!"
    "{i}Транспортер знижується на найближчий дах. На ньому вже чекають дві металеві потвори"
    "{i}Щойно бус приземляється, двері відчиняються і в них влітають двоє роботів-Перевіряючих"
    "{i}Ще декілька секунд, і їх металеві щупальця схоплять мене. Що ж робити?"


    if втеча_годіум == True:
        play sound "audio/Сцена 8/Звук Таймера.mp3"
        $ timer_jump = 'proval_brama'
        $ time = 7
        $ timer_range = 7
        show screen countdown

        menu:
            "Застосувати електрошокер":
                hide screen countdown
                play sound "audio/Сцена 8/Звук Електрошокера.mp3"
                павка "Ось тобі, потворо!"
                "Це був останній заряд електрошокеру, але який постріл!"
                "Робот з декілька разів дригається від потужного заряду, потім з металевим скавучанням падає на землю"
                "Можливо я і не врятуюсь сьогодні, але до чого ж приємно було вмазати цьому опудалу!"
                "Пасажири з острахом вжимаються у сидіння. Другий робот наближається, треба бігти!"

                play sound "audio/Сцена 8/Звук Таймера.mp3"
                menu:
                    "Вистрибнути у вікно":
                        "Вистрибнувши з вікна, я опиняюсь на даху будинку."
                        "Тут я наче на долоні. Єдиний порятунок - сходи, що ведуть вниз, у будівлю."
                        "Я забігаю в середину і роблю декілька кроків, аж тут..."
                        jump scene_eight_end

            "Відволікти увагу годіумом":
                $ renpy.notify('Gained achievement: "Годіумбольщиця "')
                hide screen countdown

                "Гей, народ! В мене тут остання плашка годіуму в місті! Нових поставок більше не буде, тож поспішайте придбати!"
                "Всі миттево забувають про свій страх перед Перевіряючими і повертаються до мене"
                "Я вимахую поцупленою пляшкою над головою, щоб довести свої слова"
                "Потім кидаю її прямо в найближчого Перевіряючого"
                "Як я і розраховувала, робот миттево перехоплює склянку в повітрі"
                "Але вже через секунду на нього накидується п'ять чи шість пасажирів. Всі інші скупчуються поряд, блокуючи прохід другому роботу"
                "Це дасть мені трохи часу"
                jump scene_eight_end


    elif втеча_годіум == False:
        play sound "audio/Сцена 8/Звук Таймера.mp3"
        $ timer_jump = 'proval_brama'
        $ time = 7
        $ timer_range = 7
        show screen countdown
        menu:
            "Застосувати електрошокер":
                hide screen countdown
                play sound "audio/Сцена 8/Звук Електрошокера.mp3"
                павка "Ось тобі, потворо!"
                "Це був останній заряд електрошокеру, але який постріл!"
                "Робот з декілька разів дригається від потужного заряду, потім з металевим скавучанням падає на землю"
                "Можливо я і не врятуюсь сьогодні, але до чого ж приємно було вмазати цьому опудалу!"
                "Пасажири з острахом вжимаються у сидіння. Другий робот наближається, треба бігти!"

                $ timer_jump = 'proval_brama'
                $ time = 7
                $ timer_range = 7
                show screen countdown
                play sound "audio/Сцена 8/Звук Таймера.mp3"
                menu:
                    "Вистрибнути у вікно":
                        hide screen countdown
                        "Вистрибнувши з вікна, я опиняюсь на даху будинку."
                        "Тут я наче на долоні. Єдиний порятунок - сходи, що ведуть вниз, у будівлю."
                        "Я забігаю в середину і роблю декілька кроків, аж тут..."
                        jump scene_eight_end



label proval_brama:
    play sound "audio/Сцена 8/Звук робота.mp3"
    "Я продовжую стояти, нерозуміючи, що мені робити..."
    перевіряючий "Підозрювана локалізована. Ініціюю процедуру захоплення"
    scene gameover with dissolve
    "Ну от і все. На цьому моя історія закінчується…"
    "..."
    "Чи?..."
    stop sound fadeout 2
    jump scene_eight


label scene_eight_end:
        stop music fadeout 3
        невідомий "Не рухайся"
        play sound "audio/Сцена 8/Звук Боротьби Придушений Зойк.mp3"
        stop music fadeout 3
        play music "audio/Сцена 8/Тема Втечі.mp3"
        "..."
        "{i}Чиїсь сильні руки міцно охоплюють мене і підіймають над землею"
        "{i}Я намагаюсь кричати, але за секунду горло слабне, а очі застилає темрява"
        stop sound
        scene фон with dissolve
        "..."
        "{i}Прощавай, світ"

        jump scene_nine
