
label scene_seven:
    scene автомат_годіум2 with dissolve
    play music "audio/Сцена 4/МістоАмбієнт2.mp3" fadeout 5

    show павка нейтральна at left2 with dissolve
    show інкі нейтральна at right2 with dissolve
    інкі "Фу-у-у-ух, я втомилась."

    show інкі весела at right2 with dissolve
    інкі "Нам пощастило: цей автомат не зламався — за нами тільки рутинний огляд."

    інкі "Перевіримо цей трансконектор — і ми вільні."

    if обіцянка_інкі == True:
        show інкі збуджена at right2 with dissolve
        інкі "Ти ж пам'ятаєш, що ти мені подвійну порцію годіум-36 обіцяла?"

        show павка нейтральна at left2 with dissolve
        павка "Ага."

    show павка задумлива at left2 with dissolve
    "{i}Мені теж треба залізти у цей автомат. Лише після цього я зможу бути по-справжньому вільна."

    show інкі нейтральна at right2 with dissolve
    інкі "Ну що ж, почнемо."

    show павка нейтральна at left2 with dissolve
    play sound "audio/Сцена 5/ЗвукВідкриттяАвтоматуГарний.mp3"
    павка "Інкі, а можна в тебе спитати щось особисте?"

    show інкі скептична at right2 with dissolve
    інкі "Що саме? А-а-а, пофіг. Валяй."

    menu:
        "Розкажи про свою сім'ю":
            show павка нейтральна at left2 with dissolve
            павка "Розкажи про свою сім'ю. Ми вже давно знайомі, але ти жодного разу про них не розповідала."

            show інкі здивована at right2 with dissolve
            інкі "А що з ними? Сім'я як сім'я."

            show інкі сумна at right2 with dissolve
            інкі "З ними інколи буває важко, але я знайшла спосіб з цим справлятись."

            if відносини_інкі < 3:
                show інкі нейтральна at right2 with dissolve
                інкі "Але то таке. Не хочу зараз про них говорити."
            else:
                show інкі весела at right2 with dissolve
                інкі "Трошки годіуму - і сімейні драми вже не такі травматичні."

                show інкі задумлива at right2 with dissolve
                інкі "Правда, останнім часом вони наче змовились всі..."

                інкі "Розумієш, через те, що я змогла вибитися в люди, вони чомусь думають, що я їм щось 'винна'."

                show інкі роздратована at right2 with dissolve
                інкі "Мама вже задовбала кожен день дзвонити і жалітись... Але це ще квіточки - інколи вона погрожує і вимагає дозу."

                show інкі сумна at right2 with dissolve
                інкі "Іноді після її істерик буває так паршиво, що ніякий годіум не допомагає."

                show павка здивована at left2 with dissolve
                павка "Боже, Інкі. Я й гадки про це не мала."

                menu:
                    "Співчуваю тобі, подруго":
                        show павка сумна at left2 with dissolve
                        $відносини_інкі +=2
                        павка "Співчуваю тобі, подруго. Дай знати, якщо я можу тобі якось допомогти."

                        show інкі розгублена at right2 with dissolve
                        інкі "Ти вже допомогла, Павка. Дякую, що вислухала."

                        show інкі задумлива at right2 with dissolve
                        $ renpy.notify('Gained achievement: "Дружня підтримка"')
                        інкі "Знаєш, вся ця твоя фігня з відмовою від годіуму - можливо щось в цьому і є. Не можу перестати думати, якою була б мама, якби перестала вживати."

                    "От бачиш, до чого доводить годіум!":
                        show павка зла at left2 with dissolve
                        павка "От бачиш, до чого доводить годіум! Якби не це прокляте зілля, у вас із мамою були б зовсім інші відносини."

                        show інкі розгублена at right2 with dissolve
                        $ renpy.notify('Gained achievement: "Серйозна розмова"')
                        інкі "Може ти і права..."

                show павка нейтральна at left2 with dissolve
                павка "В будь-якому разі, дякую що поділилась."

        "Як ти до мене ставишся?":
            show павка нейтральна at left2 with dissolve
            павка "Як ти до мене ставишся?"

            show інкі здивована at right2 with dissolve
            інкі "З чого такі питаннячка раптом?"

            show павка усміхнена at left2 with dissolve
            павка "Так, цікаво."

            show інкі весела at right2 with dissolve
            інкі "Я тобі так скажу."

            if відносини_інкі < 3:
                show інкі задумлива at right2 with dissolve
                інкі "Чесно кажучі, ти мене трохи хариш."

                show інкі скептична at right2 with dissolve
                інкі "Твої фрікуваті погляди на життя - це твоє діло. Але ти постійно намагаєшся їх нав'язувати всім оточуючим."

                show інкі роздратована at right2 with dissolve
                інкі "І це мене вже порядком так задовбало."

                show інкі нейтральна at right2 with dissolve
                інкі "Без образ."

                show павка розгублена at left2 with dissolve
                павка "Ем... Ну що ж, дякую за відвертість."

            if відносини_інкі >= 3:
                show інкі збуджена at right2 with dissolve
                інкі "Таких неордінарних поглядів на життя, як у тебе, я ще не зустрічала."

                show інкі весела at right2 with dissolve
                інкі "Ти інколи буваєш нестерпною, але з тобою точно не занудьгуєш."

                show павка нейтральна at left2 with dissolve
                павка "Дякую, мені було важливо це почути."

    show павка нейтральна at left2 with dissolve
    павка "Можна ще одне запитання?"

    show інкі нейтральна at right2 with dissolve
    інкі "Валяй. Я з тобою сьогодні наче на інтерв'ю."

    show павка задумлива at left2 with dissolve
    павка "Розкажи про свою колекцію реклами. Що вона для тебе?"

    if відносини_інкі < 3:
        show інкі здивована at right2 with dissolve
        інкі "Та, це трохи дивна звичка."

        show інкі задумлива at right2 with dissolve
        інкі "Я ніби хочу зібрати у себе весь найкращий контент."

        show інкі весела at right2 with dissolve
        інкі "Вони красиві. А мене штирить від краси."

        show інкі збуджена at right2 with dissolve
        інкі "Я вічна гонщиця в погоні за насолодою."

    if відносини_інкі >= 3:
        show інкі задумлива at right2 with dissolve
        інкі "Ті кольорові анімовані зображення і слогани… Я тобі скажу, тільки обіцяй, що не будеш сміятися."

        show павка нейтральна at left2 with dissolve
        павка "Обіцяю."

        show інкі задумлива at right2 with dissolve
        інкі "Іноді мені починає здаватися, що вони мають на мене дуже сильний вплив."

        show павка здивована at left2 with dissolve
        павка "Чому?"

        show інкі весела at right2 with dissolve
        інкі "Просто тому, що вони красиві. Я їми вже всю спальню заліпили, ніяк не можу зупинитись."

        інкі "І ось я лежу вечорами в ліжечку, споглядаю їх, і мені здається, що я найщасливіша людина в світі. Бо мене оточують такі дивні штуки."

        show інкі задумлива at right2 with dissolve
        інкі "Вони наповнюють мене відчуттям, що я не просто в лежу в одній з квартирі в одному з будинків, а знаходжуся в середині чогось більшого."

        інкі "Одне серце на всіх, одне бажання, одна пристрасть."

        show інкі весела at right2 with dissolve
        інкі "Але то таке. Незважай."

        інкі "Коротше кажучи, вони мені в кайф."

        menu:
            "Як годіум?":
                show інкі весела at right2 with dissolve
                інкі "Точно! Екстравагантна реклама — це годіум для моїх очей."

                інкі "Суміш світлових хвилей різної частоти, ідеально збалансована за допомогою компьютерних алгоритмів для отримання мною максимального задоволення."

                show павка задумлива at left2 with dissolve
                павка "Звучить так, наче ти сама просто машина."

                show інкі весела at right2 with dissolve
                інкі "Так! Я машина для праці і задоволення!"

            "Як мистецтво?":
                show інкі розгублена at right2 with dissolve
                інкі "М? Це щось на старій мові, я не розумію."

    show павка замріяна at left2 with dissolve
    павка 'Я чула, що колись люди самі створювали такі речі. Малювали, робили музику. Це називалось “мистецтвом”.'

    show інкі скептична at right2 with dissolve
    інкі "А, чула про таке. В них наче були якісь витвори, які вони витворяли. Тепер все це роблять комп'ютери. Можна не паритися і просто споживати."

    if відносини_інкі >= 3:
        show інкі задумлива at right2 with dissolve
        інкі "А що ти думаєш про це мистецтво?"

        show павка задумлива at left2 with dissolve
        павка "Я точно не знаю, що це. Але інколи мені здається, що те, що я роблю можна так назвати."

        show павка усміхнена at left2 with dissolve
        павка "І це допомагає мені не стати однією з самогубців, про яких ти читаєш у ранкових новинах."

    show павка замріяна at left2 with dissolve
    павка "Одного разу я створила щось схоже на витвір мистецтва. Я заспівала пісню, і відчутя були... просто надзвичайні."

    show павка сумна at left2 with dissolve
    павка "А потім я заспівала її близькій людині, і це врятувало її у скрутну хвилину."

    show павка рішуча at left2 with dissolve
    павка "З того часу я більше не вживаю годіум."

    show інкі здивована at right2 with dissolve
    інкі "Чому ви просто не пішли до Залу Насолоди і не повтичили в Лампу Радості?"

    show павка задумлива at left2 with dissolve
    павка "Ти не розумієш, це інше."

    show павка замріяна at left2 with dissolve
    павка "Інколи мені вдається скласти кілька звуків в єдине ціле, щоб висловити свої почуття. Тоді мене охоплює така велика радість, що всякий там годіум і поруч з нею не валявся."

    show павка весела at left2 with dissolve
    павка "Ось, послухай."

    stop music fadeout 1
    play sound "audio/Пісні Павка/Мугикання.mp3"
    show павка співає at left2 with dissolve
    "..."

    stop sound fadeout 1
    play music "audio/Сцена 4/МістоАмбієнт2.mp3" fadeout 5
    show інкі налякана at right2 with dissolve
    інкі "Стоп. Замовкни. Ти що робиш?!"

    show павка налякана at left2 with dissolve
    павка "Все, все, мовчу."

    show інкі розгублена at right2 with dissolve
    інкі "Подруго, ти дуже дивно себе поводиш. На нас люди вже дивляться."

    show павка сумна at left2 with dissolve
    павка "Вибач."

    if відносини_інкі >= 3:
        show інкі рішуча at right2 with dissolve
        інкі "Але звучить цікаво. Росповіси про це? Ввечері..."

    if відносини_інкі < 3:
        show інкі роздратована at right2 with dissolve
        інкі "Будеш і далі щось таке витворяти — я викличу перевіряючих, зрозуміло? Хто зна, може ти вже стала суспільно небезпечною?"

    play sound "audio/Сцена 5/ЗвукВідкриттяАвтомату.mp3"
    show інкі рішуча at right2 with dissolve
    інкі "Так, я закінчила!"

    if обіцянка_інкі == True:
        show інкі збуджена at right2 with dissolve
        інкі "Де там моя обіцяна подвійна порція 36-го?"

    if обіцянка_інкі == False:
        show інкі збуджена at right2 with dissolve
        інкі "Нарешті, можна залити спогади про цей тупий день черговою порцією божественного 36-го."

        if відносини_інкі >= 3:
            show інкі весела at right2 with dissolve
            інкі "Ти зі мною?"

    show павка задумлива at left2 with dissolve
    "{i}Треба залізти в цей клятий автомат і зробити свою справу. Але як не спалитися перед Інкі?"

    stop music fadeout 2
    play music "audio/Сцена 7/ТемаПередВтечею.mp3" fadeout 2

    menu:
        "Посіяти сумнів в її компетенції":
            $ відносини_інкі -= 1
            show павка рішуча at left2 with dissolve
            павка "Краще я все ще раз перевірю, бо хто зна, що ти впустила через твої годіумні зашквари."

            show інкі роздратована at right2 with dissolve
            інкі "Нащо ти таке кажеш, я у своїй справі краща, в мене жодної помилки не було — і ти про це знаєш!"

            show інкі скептична at right2 with dissolve
            інкі "Це взагалі-то ти в нас фрік, а не я!"

        "Подивитись деталь в автоматі":
            show павка рішуча at left2 with dissolve
            павка "Хочу нагадати собі, як виглядає одна деталь. Просто для загального розвитку…"

            show інкі здивована at right2 with dissolve
            інкі "Е-е-е… А це обов’язково робити саме зараз? Дивна ти…"

        "Бути відвертою":
            $ відносини_інкі += 1
            show павка рішуча at left2 with dissolve
            павка "Слухай, Інкі, мені треба зробити дещо важливе. Не можу сказати що, але від цього залежить моя подальша доля. Це не займе більше п'яти хвилин."

    if відносини_інкі >= 4:
        show інкі розгублена at right2 with dissolve
        інкі "В мене погані передчуття, але гаразд, роби що треба."

        show інкі весела at right2 with dissolve
        інкі "Але май на увазі, що я розраховую на повний звіт за чаркою годіуму сьогодні ввечері. Ти пригощаєш!"

    if відносини_інкі == 3:
        show інкі скептична at right2 with dissolve
        інкі "Щоб ти не надумала, роби це швидко."

        show інкі сумна at right2 with dissolve
        інкі "І я не хочу нічого про це знати, зрозуміло?"

    if відносини_інкі < 3:
        show інкі скептична at right2 with dissolve
        інкі "Ти сьогодні себе весь день дивно поводиш, я так і знала, що ти щось задумала."

        show інкі роздратована at right2 with dissolve
        інкі "Мабуть без годіуму зовсім дах їде, вирішила загарбати собі зайву пляшечку за рахунок компанії, е?"

        інкі "До автомати ти підійдеш через мій труп, зрозуміла?"

        menu:
            "Застосувати електрошокер":
                $ електрошок += 1
                $шок_інкі = True
                $відносини_інкі = 0
                show павка сумна at left2 with dissolve
                павка "Вибач, Інкі."

                show інкі налякана at right2 with dissolve
                інкі "ГЕЙ, ти що зовсім здуріла???!"

                play sound "audio/Сцена 7/Електрошокер.mp3"
                hide інкі налякана with dissolve
                show павка рішуча at left2 with dissolve
                "{i}Чого не зробиш заради свободи. Довелося збільшити потужність, щоб її вирубило, а не просто полоскотало, як того придурка."

                "{i}В мене є десять хвилин доки вона не оговталась і мене не помітили."

            "Спробувати вмовити":
                show павка рішуча at left2 with dissolve
                павка "Інкі, ти не розумієш. Це щось дуже важливе для мене. Пусти мене, будь ласка!"

                show інкі роздратована at right2 with dissolve
                інкі "Я не для того стільки років працювала як робот і вибивалась в люди, щоб все це було похерено в один день якоюсь божевільною невживанкою! А ну геть від автомату!"

                show інкі рішуча at right2 with dissolve
                інкі "Я викликаю перевіряючих!"

                show павка розгублена at left2 with dissolve
                "{i}Вона тягнеться за рацією. Треба діяти швидко, інакше про свободу можна забути."

                menu:
                    "Застосувати електрошокер":
                        $ електрошок += 1
                        $шок_інкі = True
                        $відносини_інкі = 0
                        show павка сумна at left2 with dissolve
                        павка "Вибач, Інкі."

                        show інкі налякана at right2 with dissolve
                        інкі "ГЕЙ, ти що зовсім здуріла???!"

                        show павка рішуча at left2 with dissolve
                        play sound "audio/Сцена 7/Електрошокер.mp3"
                        hide інкі налякана with dissolve
                        "{i}Чого не зробиш заради свободи. Довелося збільшити потужність, щоб її вирубило, а не просто полоскотало, як того придурка."

                        "{i}В мене є десять хвилин доки вона не оговталась і мене не помітили."

                    "Остання спроба порозумітись!":
                        $відносини_інкі += 1
                        show павка рішуча at left2 with dissolve
                        павка "Інкі, життя поза межами Купола існує! Життя без годіуму і без Оази. Без необхідності витрачати себе на безглузду роботу, перемежовану відчайдушним наркотичним забуттям."

                        show павка рішуча at left2 with dissolve
                        павка "Я хочу спробувати побачити цей світ. Чи померти, намагаючись це зробити. Бо жити так, як зараз я більше не можу."

                        show павка сумна at left2 with dissolve
                        павка "Розмієш?"

                        if відносини_інкі >= 3:
                            show інкі сумна at right2 with dissolve
                            інкі "Павко, це піздець. Ти серйозно в це віриш?"

                            show павка сумна at left2 with dissolve
                            павка "Так. Це мій єдиний шанс на порятунок. Я по твоїм очам бачу, що ти мене розумієш."

                            show інкі роздратована at right2 with dissolve
                            інкі "Бля! Лайно! Блядське лайно!"

                            show інкі розгублена at right2 with dissolve
                            інкі "Гаразд, але щоб ти не задумала, роби це швидко."

                            show інкі скептична at right2 with dissolve
                            інкі "І я не хочу нічого про це знати, зрозуміло?"

                        if відносини_інкі < 3:
                            show інкі скептична at right2 with dissolve
                            інкі "Непогана спроба, кицюне."

                            інкі "Але мене цими казками не обдуриш."

                            show інкі рішуча at right2 with dissolve
                            інкі "Прийом, це технічний юніт 45-6. В мене критична ситуація, спроба пограбування трансконнектору №18б. Терміново виїжджайте!"

                            show павка сумна at left2
                            show інкі рішуча at right2
                            scene gameover with dissolve
                            hide інкі рішуча with dissolve
                            hide павка сумна with dissolve
                            "{i}Ну от і все. На цьому моя історія закінчується."

                            jump scene_seven

    scene фон with dissolve
    "..."

    "{i}Минуло 5 хвилин"

    scene автомат_годіум2 with dissolve
    if шок_інкі == False:
        show інкі нейтральна at right2

    show павка весела at left2 with dissolve
    "..."

    павка "Ну от і все! Наш робочий день закінчено!"

    if шок_інкі == False:

        if відносини_інкі >= 4 and годіум_інкі == False:

            show інкі задумлива at right2 with dissolve
            інкі "Слухай, Павко, я здогадалася, що ти не плануєш мене пригощати 36-м — це не в твоєму стилі. Але перш ніж ми розійдемося, я б хотіла тобі дещо розказати."

            show інкі розгублена at right2 with dissolve
            інкі "В мене реально великі проблеми в сім'ї. Кожного вечора мама дзвонить і вимагає годіум."

            інкі "Я не знаю, чого я боюсь більше: її чергової істерики..."

            інкі "Чи того, що завтра вранці прочитаю в новинах про її самогубство."

            show інкі сумна at right2 with dissolve
            інкі "Ти не могла б провести зі мною цей вечір? Хоч трохи, хоч пару годин…"

            show інкі налякана at right2 with dissolve

        if відносини_інкі >= 4 and годіум_інкі == True:
            show інкі задумлива at right2 with dissolve
            інкі "Подруго! Я хочу тобі дещо розповісти."

            show інкі сумна at right2 with dissolve
            інкі "Сльози стоять в мене в очах, мені дуже важко з цим жити!"

            show інкі розгублена at right2 with dissolve
            інкі "Мама вимагає від мене годіум. Вона мене вже дістала, кожен вечір дзвонить."

            інкі "Я нічого не розумію і мені страшно. Можна я просто поплачу на твоєму плечі?"

            show павка розгублена at left2 with dissolve
            павка "Та плач…"

            show інкі сумна at right2 with dissolve
            інкі "Плачу-у-у-у-у!...."

            show інкі збуджена at right2 with dissolve
            інкі "Ну все, пішли закидатися 36-м!"

            show інкі налякана at right2 with dissolve

        if відносини_інкі < 4 and годіум_інкі == True:
            show інкі рішуча at right2 with dissolve
            інкі "Ти просто крейзі вумен! Я тут намагаюся з тобою подружитися, а ти фігнею маєшся."

            show інкі скептична at right2 with dissolve
            інкі "Дівка чудить без дози."

            show інкі рішуча at right2 with dissolve
            інкі "В тебе зовсім мізки без речовин перегоріли?"

            show інкі здивована at right2 with dissolve
            інкі "Гей народе, моя напарниця щось витворяє з автоматом — мабуть в неї дах з'їхав!"

            show інкі налякана at right2 with dissolve

        if відносини_інкі < 4 and годіум_інкі == False:
            show інкі скептична at right2 with dissolve
            інкі "Павко, я не знаю, що ти задумала, і нащо копирсалася в автоматі, але все це супер підозріло."

            show інкі задумлива at right2 with dissolve
            $ renpy.notify('Gained achievement: "Нічого особистого"')
            інкі "Нічого особистого, але я доповім про це керівництву!"

            show інкі налякана at right2 with dissolve

    play music "audio/Сцена 2/ТемаЛайі.mp3" fadeout 2
    scene автомат_годіум тривога with dissolve

    if шок_інкі == False:
        show інкі здивована at right2 with dissolve

    show павка здивована at left2 with dissolve
    голос "Громадяни Купола! З вами говорить організація Антарес."

    if шок_інкі == False:
        show інкі розгублена at right2 with dissolve

    show павка задумлива at left2 with dissolve
    "{i}Чорт, це зовсім не схоже на простий збір інформації…"

    show павка налякана at left2 with dissolve
    голос "Купка еліт в Оазі наживається на стражданнях та злиднях мільйонів жителів периферії."

    show павка задумлива at left2 with dissolve

    if шок_інкі == False:
        show інкі налякана at right2 with dissolve

    голос "Вони використовують годіум, щоб промивати вам мізки та приховувати від вас правду."

    show павка налякана at left2 with dissolve
    голос "За межами Купола є життя, ви можете бути вільними від їх тиранії."

    show павка зла at left2 with dissolve
    голос "Але щоб це зрозуміти, вам спочатку треба протверезіти."

    show павка рішуча at left2 with dissolve

    if шок_інкі == False:
        show інкі нейтральна at right2 with dissolve

    голос "З цієї миті подача годіуму зупиняється у всіх автоматах. Це початок кінця."

    голос "Геть тиранію Оази!"

    show павка здивована at left2 with dissolve
    "{i}Що? Що це в біса означає?!"

    "{i}Мене підставили!"

    menu:
        "Треба бігти":
            show павка налякана at left2 with dissolve
            "{i}Треба бігти!"

        "Довбаний Лайя!":
            show павка зла at left2 with dissolve
            "{i}Довбаний Лайя!"

    stop music fadeout 3
    play music "audio/Сцена 7/Тривога.mp3" fadeout 1

    if шок_інкі == False:
        show інкі налякана at right2 with dissolve

    show павка налякана at left2 with dissolve
    гучномовець "УВАГА. УВАГА. ВЧИНЕНО ДИВЕРСІЮ. ВИКОРИСТАННЯ АЕРОТРАНСПОРТУ ПРИЗУПИНЕНО!"

    гучномовець "РОЗХОДЬТЕСЯ ПО СВОЇХ ДОМІВКАХ І ЧЕКАЙТЕ НА ПЕРЕВІРЯЮЧИХ. ЗАБОРОНЕНО ЗАЛИШАТИ ОАЗУ, ДОКИ ДИВЕРСАНТА НЕ БУДЕ ЗНАЙДЕНО!"

    show павка задумлива at left2 with dissolve

    if шок_інкі == False:
        show інкі нейтральна at right2 with dissolve

    "{i}Запізно."

    if шок_інкі == False:
        show інкі розгублена at right2 with dissolve
        інкі "Це якась нісенітниця. Цього не може бути!"

        інкі "Я сплю і це мій страшний сон, навіянний тією дивачкою. Я зараз прокинуся і все буде добре."

        show павка задумлива at left2 with dissolve
        "{i}Що ж мені робити?"

        show інкі задумлива at right2 with dissolve
        інкі "Це ти зробила. І це ніякий не сон…"

        if відносини_інкі < 4 and годіум_інкі == False:
            show інкі роздратована at right2 with dissolve
            інкі "Ах ти тварюка, я так і знала, що ти хотіла нас усіх залишити без годіуму!"

            інкі "Я викликаю перевіряючих."

            hide інкі скептична with dissolve

        if відносини_інкі < 4 and годіум_інкі == True:
            show інкі розгублена at right2 with dissolve
            інкі "Чорт, я не знаю, що зараз відбувається, і не хочу мати з цим нічого спільного."

            інкі "Я просто сумлінно виконувала свою роботу, а тут ця невживанка влаштувала діверсію."

            show інкі сумна at right2 with dissolve
            інкі "Просто посиджу тут, поки перевіряючі не прилетять… Вони все зрозуміють, я ж нічого поганого не робила!"

            hide інкі сумна with dissolve

        if відносини_інкі >= 4 and годіум_інкі == True:
            show інкі роздратована at right2 with dissolve
            $прикриття_інкі = True
            інкі "Що за лайно! Я вже не витримую…! Спочатку мама, тепер це!"

            інкі "Нехай все воно горить вогнем! Як же я ненавиджу це місто!"

            show інкі рішуча at right2 with dissolve
            інкі "Тікай, прекрасна дивачко! Біжи за своєю шаленою мрією та не обертайся!!"

            show інкі збуджена at right2 with dissolve
            інкі "Гей ви, потвори, візьміть мене!! Це я зробила, чуєте!"

            show павка здивована at left2 with dissolve
            павка "Ееее… як скажеш, подруго."

            hide інкі збуджена with dissolve

        if відносини_інкі >= 4 and годіум_інкі == False:
            show інкі сумна at right2 with dissolve
            $прикриття_інкі = True
            інкі "Чорт, я не знаю, що відбувається, але я з тобою, Павко."

            show інкі розгублена at right2 with dissolve
            інкі "Напевно, ти маєш свої причини робити те, що робиш."

            інкі "Я знаю, що таке важке життя, знаю, що таке мати проблеми. Ні в чому тебе не звинувачую."

            show інкі весела at right2 with dissolve
            інкі "Я тебе не здам. Біжи, я прикрию."

            show павка усміхнена at left2 with dissolve
            павка "Дякую, Інкі! Я цього не забуду."

            hide інкі весела with dissolve

    show павка рішуча at center with dissolve
    "{i}Час тікати."

    scene фон
    "..."
    jump scene_eight
